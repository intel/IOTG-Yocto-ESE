DISTRO = "intel-embedded-system-enabling"
DISTRO_NAME = "meta-intel-ese Reference Distro"
DISTRO_VERSION = "1.0-ESE"
DISTRO_CODENAME = "${LAYERSERIES_CORENAMES}"

SDK_VENDOR = "-esesdk"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

TARGET_VENDOR = "-ese"
LOCALCONF_VERSION = "1"

# Providers
PREFERRED_PROVIDER_jpeg = "libjpeg-turbo"
PREFERRED_PROVIDER_jpeg-native = "libjpeg-turbo-native"

# Default features enabled in distro
ESE_DEFAULT_DISTRO_FEATURES = "wayland opengl gles2 egl pam systemd ptest virtualization"
ESE_DEFAULT_EXTRA_RDEPENDS = "packagegroup-core-boot"

DISTRO_FEATURES_DEFAULT ?= "acl alsa argp bluetooth ext2 largefile usbgadget usbhost wifi xattr pci"
DISTRO_FEATURES_NATIVE ?= "ipv6"
DISTRO_FEATURES_NATIVESDK ?= "libc-charsets libc-locales libc-locale-code"
GTK2DISTROFEATURES ?= "directfb"
GTK3DISTROFEATURES ?= "wayland"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${DISTRO_FEATURES_LIBC} ${ESE_DEFAULT_DISTRO_FEATURES}"
DISTRO_EXTRA_RDEPENDS += "${ESE_DEFAULT_EXTRA_RDEPENDS}"

VIRTUAL-RUNTIME_init_manager = '${@bb.utils.contains("DISTRO_FEATURES", "systemd", "systemd", "sysvinit", d)}'
VIRTUAL-RUNTIME_initscripts = '${@bb.utils.contains("DISTRO_FEATURES", "systemd", "", "initscripts",d)}'
DISTRO_FEATURES_BACKFILL_CONSIDERED:append =' ${@bb.utils.contains("DISTRO_FEATURES", "systemd", "sysvinit", "", d)}'

# Inherit dm-verity class which will include dm-verity feature
# conditional upon dm-verity being included in DISTRO_FEATURES
INHERIT += "dm-verity"

# for x86-64
require ${@bb.utils.contains("TARGET_ARCH", "x86_64", "conf/include/ese-x86-all.inc", "", d)}
